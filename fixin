#!/usr/bin/env perl
# fixin - convert indentation to your preferred format
# James Halliday (http://substack.net)
# vim:ts=4:sw=4:tw=80:et

# TODO:
# * guess indentation level for files without modelines (with --guess)
# * parse per-file indentation preferences in .vimrc
# * load in global /etc/vimrc
# * better support for gvim
# * emacs

use warnings;
use strict;

use 5.10.0;
use Getopt::Casual;

my $usage = <<EOF;
    Convert and restore the leading whitespace in FILE to your preferred
    indentation style.
    
    Usage: $0 OPTIONS FILE
        
    If file is omitted or is '-', use stdin.
        
    Where OPTIONS are:
        --convert
            Convert whitespace in a file with a modeline to preferred
            format. (default action)
        --restore
            Inverse of --convert: revert from preferred whitespace to the
            format in the modeline.
        
        --vimrc=file
            use this vimrc file (default: \$HOME/.vimrc)
            
        --et, --expandtab
            Set expand tab preference. (overrides vimrc)
        --ts, --tabstop
            Set tab stop preference. (overrides vimrc)
         
        --help, -h
            Show this message.
EOF

($ARGV{"--help"} or $ARGV{"-h"}) and die $usage;
$ARGV{"--vimrc"} //= "$ENV{HOME}/.vimrc";

use Tie::File;
my $filename = shift // "-";

my @file;
if ($filename eq "-") {
    @file = <>;
}
else {
    -e $filename or die "File '$filename' doesn't exist";
    tie @file, 'Tie::File', $filename
        or die "Couldn't open '$filename' failed: $!";
}

@file or exit; # don't mess with empty files

# Formatting for modelines is documented here:
# http://vimdoc.sourceforge.net/htmldoc/options.html#auto-setting
# [text]{white}{vi:|vim:|ex:}[white]se[t] {options}:[text]
# [text]{white}{vi:|vim:|ex:}[white]{options}

my $re = qr{
    \s+ (?: vi | vim | ex ) : \s?
    (?: (?: set | se\  ) (.+) : | (.+) )
}x;

my %modes;
for my $opts (grep defined, map $_ =~ $re, grep defined, @file[0..4]) {
    if (my @params = split m/(?<! \\) [:\ ] \s* (?: set \s*)? /x, $opts) {
        %modes = map {
            if (m/(?<! \\) = /x) {
                m/([^=]+)=(.+)/ # assignment
            }
            else {
                (my $flag = $_) =~ s/^no//;
                $flag => m/^no/ ? 0 : 1 # boolean flags
            }
        } @params;
        last;
    }
}
%modes || do {
    # only process when modeline is found
    print for grep defined, @file;
    exit;
};

# short-form aliases
$modes{tabstop} = int($modes{tabstop} // $modes{ts} // 8);
$modes{expandtab} //= $modes{et} // 0;
$ARGV{"--expandtab"} //= $ARGV{"--et"};
$ARGV{"--tabstop"} //= $ARGV{"--ts"};

my %prefs;

unless (defined $ARGV{"--expandtab"} and defined $ARGV{"--tabstop"}) {
    tie my @vimrc, "Tie::File", $ARGV{"--vimrc"} or die "Couldn't open vimrc: $!";
    %prefs = map {
        if (m/(?<! \\) =/x) {
            m/([^=]+)=(.+)/ # assignment
        }
        else {
            (my $flag = $_) =~ s/^no//;
            $flag => m/^no/ ? 0 : 1 # boolean flags
        }
    } grep defined, map m/^set \s+ (.+)/x, @vimrc;
    untie @vimrc;
}

$prefs{expandtab} = $ARGV{"--expandtab"} // $prefs{expandtab} // $prefs{et};
$prefs{tabstop} = int($ARGV{"--tabstop"} // $prefs{tabstop} // $prefs{ts} // 8);

if ($prefs{expandtab} == $modes{expandtab}
and $prefs{tabstop} == $modes{tabstop}) {
    print for grep defined, @file;
    exit;
}

if ($ARGV{"--restore"}) {
    # convert backwards for resets
    my $prefs_r = { %prefs };
    my $modes_r = { %modes };
    %modes = %$prefs_r;
    %prefs = %$modes_r;
}

(tied @file)->defer if $filename ne "-";
for my $line (grep defined, @file) {
    if ($prefs{expandtab}) {
        my $mspaces = " " x $modes{tabstop};
        my $pspaces = " " x $prefs{tabstop};
        
        1 while $line =~ s/^(\s*?)\t/$1$mspaces/;
        
        $line =~ s[^((?:$mspaces)+)]
            [ $pspaces x ((length $1) / $modes{tabstop}) ]e;
    }
    else {
        my $spaces = " " x $prefs{tabstop};
        1 while $line =~ s/^(\s*?)$spaces/$1\t/
    }
}
(tied @file)->flush if $filename ne "-";

if ($filename eq "-") {
    print for grep defined, @file;
}
else {
    untie @file;
}
